class_name PlayerDataPacket

enum PACKET_TYPE {
	PlayerPacket
}

const HEADER_SIZE = 10

var packet_type: int = PACKET_TYPE.PlayerPacket
var id: int
var position: Vector2
var animation_name: String
var flag: int = ENetPacketPeer.FLAG_UNSEQUENCED

static func create(_id: int, _position: Vector2, _animation_name: String) -> PlayerDataPacket:
	var new_packet = PlayerDataPacket.new()
	new_packet.id = _id
	new_packet.position = _position
	new_packet.animation_name = _animation_name
	return new_packet

static func create_from_data(data: PackedByteArray) -> PlayerDataPacket:
	var new_packet: PlayerDataPacket = PlayerDataPacket.new()
	new_packet.decode(data)
	return new_packet

func decode(data: PackedByteArray) -> void:
	packet_type = data.decode_u8(0)
	id = data.decode_u8(1)
	position = Vector2(data.decode_float(2), data.decode_float(6))
	var string_data = data.slice(10)
	animation_name = string_data.get_string_from_utf8()
	return

func encode() -> PackedByteArray:
	var new_array: PackedByteArray
	var animation: PackedByteArray = animation_name.to_utf8_buffer()
	new_array.resize(HEADER_SIZE)
	new_array.encode_u8(0, packet_type)
	new_array.encode_u8(1, id)
	new_array.encode_float(2, position.x)
	new_array.encode_float(6, position.y)
	new_array.append_array(animation)
	return new_array

func send(target: ENetPacketPeer) -> void:
	target.send(0, encode(), flag)

func broadcast(server: ENetConnection) -> void:
	server.broadcast(0, encode(), flag)
